# Maintainer:  Dave Murphy <davem@devkitpro.org>
# Contributor: Cpasjuste <cpasjuste@gmail.com>
pkgbasename=libmpv
pkgname=switch-${pkgbasename}
pkgver=0.36.0
pkgrel=4
pkgdesc='Command line video player (library only)'
arch=('any')
url='https://mpv.io/'
license=('GPL')
options=(!strip libtool staticlibs)
source=("${pkgbasename}-${pkgver}.tar.gz::https://github.com/mpv-player/mpv/archive/v${pkgver}.tar.gz" "mpv.patch")
sha256sums=(
  '29abc44f8ebee013bb2f9fe14d80b30db19b534c679056e4851ceadf5a5e8bf6'
  'ae4755a1e854508e04889d3f66b21d1333d8908d98610eb1d1ef6efb5a53e8fc'
)
makedepends=("dkp-toolchain-vars" "dkp-meson-scripts")
depends=('switch-sdl2' 'switch-ffmpeg' 'switch-mesa' 'switch-liblua51')
groups=('switch-portlibs')

prepare() {
  cd mpv-$pkgver
  patch -Np1 -i "$srcdir/mpv.patch"
}

build() {
  cd mpv-$pkgver

  /opt/devkitpro/meson-cross.sh switch ../crossfile.txt \
    -Dlibmpv=true -Dcplayer=false -Dtests=false \
    -Dplain-gl=enabled -Dsdl2=enabled -Dsdl2-gamepad=enabled \
    -Dlibplacebo=disabled -Dlibarchive=disabled -Djpeg=disabled \
    -Degl=disabled -Diconv=disabled -Dlua=enabled build

  meson compile -C build
}

package() {
  cd mpv-$pkgver

  DESTDIR="$pkgdir" meson install -C build
}
